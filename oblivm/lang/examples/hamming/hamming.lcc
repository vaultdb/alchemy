package com.oblivm.backend.generated.hamming;

struct Pair<T1, T2> {
	T1 left;
	T2 right;
};

struct bit {
	int1 v;
};

struct Int@n {
	int@n v;
};

Pair<bit, Int@n> add@n(int@n x, int@n y) {
	bit cin;
	Int@n ret;
	bit t1, t2;
	int1 zzz;
	for(public int32 i=0; i<n; i = i+1) {
		zzz = x$i$;
		t1.v = zzz ^ cin.v;
		t2.v = y$i$ ^ cin.v;
		ret.v$i$ = x$i$ ^ t2.v;
		t1.v = t1.v & t2.v;
		cin.v = cin.v ^ t1.v;
	}
	return Pair{bit, Int@n}(cin, ret);
}
		
int@log(n+1) countOnes@n(int@n x) {
  if(n==1) return x;
  int@log(n/2+1) first = countOnes@(n/2)(x$0~n/2$);
  int@log(n-n/2+1) second = countOnes@(n-n/2)(x$n/2~n$);
  int@log(n+1) r;
  if(log(n/2+1)==log(n-n/2+1)) {
	  Pair<bit, Int@log(n/2+1)> ret = add@log(n/2+1)(first, second);
	  r = ret.right.v;
	  r$log(n+1)-1$ = ret.left.v;
  } else {
	  Pair<bit, Int@log(n-n/2+1)> ret = add@log(n-n/2+1)(first, second);
	  r = ret.right.v;
  }
  return r;
}


int@log(n) hamming@n(int@n x, int@n y) {
	int@n z = x ^ y;
	return countOnes@n(z);
}

int@log(n) main@n@m(int@n x, int@m y) {
	return hamming@n(x, y);
}

